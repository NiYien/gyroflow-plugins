set windows-shell := ["powershell.exe", "-NoLogo", "-Command"]

TargetDir := env_var_or_default("CARGO_TARGET_DIR", "target")

[windows]
release:
    cargo build --release
    Copy-Item -Path "{{TargetDir}}\release\gyroflow_ofx.dll" -Destination "C:\Program Files\Common Files\OFX\Plugins\Gyroflow.ofx.bundle\Contents\Win64\Gyroflow.ofx" -Force

[windows]
deploy:
    #!powershell
    $ErrorActionPreference = 'Stop'

    cargo build --profile deploy
    cd "{{TargetDir}}"
    Remove-Item -Recurse -Force gyroflow-ofx-windows, Gyroflow-OpenFX-windows.zip -ErrorAction SilentlyContinue
    New-Item -ItemType Directory -Path "gyroflow-ofx-windows/Gyroflow.ofx.bundle/Contents/Win64" -Force
    cp "deploy/gyroflow_ofx.dll" "gyroflow-ofx-windows/Gyroflow.ofx.bundle/Contents/Win64/Gyroflow.ofx"
    cp "{{justfile_directory()}}/res/Info.plist" "gyroflow-ofx-windows/Gyroflow.ofx.bundle/Contents/"
    cp "{{justfile_directory()}}/../LICENSE" "gyroflow-ofx-windows/Gyroflow.ofx.bundle/Contents/"
    cd gyroflow-ofx-windows ; 7z a ../Gyroflow-OpenFX-windows.zip .

[macos]
deploy:
    #!/bin/bash
    set -e

    rustup target add aarch64-apple-darwin
    rustup target add x86_64-apple-darwin

    cargo build --profile deploy --target x86_64-apple-darwin
    cargo build --profile deploy --target aarch64-apple-darwin

    version=$(grep '^version' Cargo.toml | sed -E 's/version = "(.+)".*/\1/')
    version_short=$(echo $version | sed -E 's/-.*//')

    if [[ "$GITHUB_REF" == *"tags"* ]]; then
        version="${version}.0"
        version_short="${version_short}.0"
    else
        if [[ -z "$GITHUB_RUN_NUMBER" ]]; then
            version="${version}-dev"
            version_short="${version_short}.1"
        else
            version="${version}.${GITHUB_RUN_NUMBER}"
            version_short="${version_short}.${GITHUB_RUN_NUMBER}"
        fi
    fi

    cd "{{TargetDir}}"
    rm -Rf gyroflow-ofx-macos Gyroflow-OpenFX-macos.dmg
    mkdir -p gyroflow-ofx-macos/Gyroflow.ofx.bundle/Contents/MacOS

    lipo {x86_64,aarch64}-apple-darwin/deploy/libgyroflow_ofx.dylib -create -output gyroflow-ofx-macos/Gyroflow.ofx.bundle/Contents/MacOS/Gyroflow.dylib
    cp "{{justfile_directory()}}/res/Info.plist" gyroflow-ofx-macos/Gyroflow.ofx.bundle/Contents/
    cp "{{justfile_directory()}}/../LICENSE"     gyroflow-ofx-macos/Gyroflow.ofx.bundle/Contents/

    /usr/libexec/PlistBuddy -c "add CFBundleVersion string $version_short" gyroflow-ofx-macos/Gyroflow.ofx.bundle/Contents/Info.plist
    /usr/libexec/PlistBuddy -c "add CFBundleShortVersionString string $version" gyroflow-ofx-macos/Gyroflow.ofx.bundle/Contents/Info.plist

    codesign -vvvv --strict --options=runtime --timestamp --force -s ${SIGNING_FINGERPRINT} gyroflow-ofx-macos/Gyroflow.ofx.bundle/Contents/MacOS/Gyroflow.dylib
    mv gyroflow-ofx-macos/Gyroflow.ofx.bundle/Contents/MacOS/Gyroflow.dylib gyroflow-ofx-macos/Gyroflow.ofx.bundle/Contents/MacOS/Gyroflow.ofx

    codesign -vvvv --deep --strict --options=runtime --timestamp --force -s ${SIGNING_FINGERPRINT} gyroflow-ofx-macos/Gyroflow.ofx.bundle
    codesign -vvvv --deep --verify gyroflow-ofx-macos/Gyroflow.ofx.bundle

    ln -sf /Library/OFX/Plugins "gyroflow-ofx-macos/"
    hdiutil create "Gyroflow-OpenFX-macos.dmg" -volname "Gyroflow OpenFX plugin" -fs HFS+ -srcfolder "gyroflow-ofx-macos/" -ov -format UDZO -imagekey zlib-level=9

    codesign -vvvv --strict --options=runtime --timestamp --force -s ${SIGNING_FINGERPRINT} "Gyroflow-OpenFX-macos.dmg"
    codesign -vvvv --deep --verify "Gyroflow-OpenFX-macos.dmg"

[linux]
deploy:
    #!/bin/bash
    set -e

    cargo build --profile deploy
    cd "{{TargetDir}}"
    rm -Rf gyroflow-ofx-linux Gyroflow-OpenFX-linux.zip
    mkdir -p gyroflow-ofx-linux/Gyroflow.ofx.bundle/Contents/Linux-x86-64
    cp deploy/libgyroflow_ofx.so gyroflow-ofx-linux/Gyroflow.ofx.bundle/Contents/Linux-x86-64/Gyroflow.ofx
    cp "{{justfile_directory()}}/res/Info.plist" gyroflow-ofx-linux/Gyroflow.ofx.bundle/Contents/
    cp "{{justfile_directory()}}/../LICENSE"     gyroflow-ofx-linux/Gyroflow.ofx.bundle/Contents/
    cd gyroflow-ofx-linux && zip -r ../Gyroflow-OpenFX-linux.zip .
