set windows-shell := ["powershell.exe", "-NoLogo", "-Command"]

TargetDir := env_var_or_default("CARGO_TARGET_DIR", "target")

[windows]
release:
    cargo build --release
    Copy-Item -Path "{{TargetDir}}\release\gyroflow_ofx.dll" -Destination "C:\Program Files\Common Files\OFX\Plugins\Gyroflow.ofx.bundle\Contents\Win64\Gyroflow.ofx" -Force

[windows]
deploy:
    #!powershell
    cargo build --profile deploy
    cd "{{TargetDir}}"
    Remove-Item -Recurse -Force gyroflow-ofx-windows, Gyroflow-OpenFX-windows.zip -ErrorAction SilentlyContinue
    New-Item -ItemType Directory -Path "gyroflow-ofx-windows/Gyroflow.ofx.bundle/Contents/Win64" -Force
    cp "deploy/gyroflow_ofx.dll" "gyroflow-ofx-windows/Gyroflow.ofx.bundle/Contents/Win64/Gyroflow.ofx"
    cp "{{justfile_directory()}}/res/Info.plist" "gyroflow-ofx-windows/Gyroflow.ofx.bundle/Contents/"
    cp "{{justfile_directory()}}/../LICENSE" "gyroflow-ofx-windows/Gyroflow.ofx.bundle/Contents/"
    cd gyroflow-ofx-windows ; 7z a ../Gyroflow-OpenFX-windows.zip .

[macos]
deploy:
    #!/bin/bash
    set -e
    cargo build --profile deploy --target x86_64-apple-darwin
    cargo build --profile deploy --target aarch64-apple-darwin
    cd "{{TargetDir}}"
    rm -Rf gyroflow-ofx-macosx gyroflow-ofx-macosx.dmg
    mkdir -p gyroflow-ofx-macosx/Gyroflow.ofx.bundle/Contents/MacOS

    lipo {x86_64,aarch64}-apple-darwin/deploy/libgyroflow_ofx.dylib -create -output gyroflow-ofx-macosx/Gyroflow.ofx.bundle/Contents/MacOS/Gyroflow.dylib
    cp "{{justfile_directory()}}/res/Info.plist" gyroflow-ofx-macosx/Gyroflow.ofx.bundle/Contents/
    cp "{{justfile_directory()}}/../LICENSE"     gyroflow-ofx-macosx/Gyroflow.ofx.bundle/Contents/

    codesign -vvvv --strict --options=runtime --timestamp --force -s ${SIGNING_FINGERPRINT} gyroflow-ofx-macosx/Gyroflow.ofx.bundle/Contents/MacOS/Gyroflow.dylib
    mv gyroflow-ofx-macosx/Gyroflow.ofx.bundle/Contents/MacOS/Gyroflow.dylib gyroflow-ofx-macosx/Gyroflow.ofx.bundle/Contents/MacOS/Gyroflow.ofx

    codesign -vvvv --deep --strict --options=runtime --timestamp --force -s ${SIGNING_FINGERPRINT} gyroflow-ofx-macosx/Gyroflow.ofx.bundle
    codesign -vvvv --deep --verify gyroflow-ofx-macosx/Gyroflow.ofx.bundle

    ln -sf /Library/OFX/Plugins "gyroflow-ofx-macosx/"
    hdiutil create "Gyroflow-OpenFX.dmg" -volname "Gyroflow OpenFX plugin" -fs HFS+ -srcfolder "gyroflow-ofx-macosx/" -ov -format UDZO -imagekey zlib-level=9

    codesign -vvvv --strict --options=runtime --timestamp --force -s ${SIGNING_FINGERPRINT} "Gyroflow-OpenFX.dmg"
    codesign -vvvv --deep --verify "Gyroflow-OpenFX.dmg"

[linux]
deploy:
    cargo build --profile deploy
    cd "{{TargetDir}}"
    rm -Rf gyroflow-ofx-linux gyroflow-ofx-linux.zip
    mkdir -p gyroflow-ofx-linux/Gyroflow.ofx.bundle/Contents/Linux-x86-64
    cp deploy/libgyroflow_ofx.so gyroflow-ofx-linux/Gyroflow.ofx.bundle/Contents/Linux-x86-64/Gyroflow.ofx
    cp "{{justfile_directory()}}/res/Info.plist" gyroflow-ofx-linux/Gyroflow.ofx.bundle/Contents/
    cp "{{justfile_directory()}}/../LICENSE"     gyroflow-ofx-linux/Gyroflow.ofx.bundle/Contents/
    cd gyroflow-ofx-linux && zip -r ../gyroflow-ofx-linux.zip .
