set windows-shell := ["powershell.exe", "-NoLogo", "-Command"]

TargetDir := env_var_or_default("CARGO_TARGET_DIR", "target")
#export RUSTFLAGS := "-Zmacro-backtrace"

[windows]
release:
    cargo build --release
    Copy-Item -Force '{{TargetDir}}\release\gyroflow_adobe.dll' '{{TargetDir}}\release\Gyroflow.aex'
    Start-Process PowerShell -Verb runAs -ArgumentList "-command Copy-Item -Force '{{TargetDir}}\release\gyroflow_adobe.dll' 'C:\Program Files\Adobe\Common\Plug-ins\7.0\MediaCore\Gyroflow.aex'"

[windows]
build:
    cargo build
    Start-Process PowerShell -Verb runAs -ArgumentList "-command Copy-Item -Force '{{TargetDir}}\debug\gyroflow_adobe.dll' 'C:\Program Files\Adobe\Common\Plug-ins\7.0\MediaCore\Gyroflow.aex'"

[windows]
deploy:
    #!powershell
    $ErrorActionPreference = 'Stop'

    cargo build --profile deploy
    cd "{{TargetDir}}"
    Copy-Item -Force "deploy\gyroflow_adobe.dll" "Gyroflow-Adobe-windows.aex"

[macos]
deploy:
    #!/bin/bash
    set -e

    rustup target add aarch64-apple-darwin
    rustup target add x86_64-apple-darwin

    cargo build --profile deploy --target x86_64-apple-darwin
    cargo build --profile deploy --target aarch64-apple-darwin

    version=$(grep '^version' Cargo.toml | sed -E 's/version = "(.+)".*/\1/')
    version_short=$(echo $version | sed -E 's/-.*//')

    if [[ "$GITHUB_REF" == *"tags"* ]]; then
        version="${version}.0"
        version_short="${version_short}.0"
    else
        if [[ -z "$GITHUB_RUN_NUMBER" ]]; then
            version="${version}-dev"
            version_short="${version_short}.1"
        else
            version="${version}.${GITHUB_RUN_NUMBER}"
            version_short="${version_short}.${GITHUB_RUN_NUMBER}"
        fi
    fi

    cd "{{TargetDir}}"
    rm -Rf gyroflow-adobe-macos Gyroflow-Adobe-macos.dmg
    mkdir -p gyroflow-adobe-macos/Gyroflow.plugin/Contents/Resources
    mkdir -p gyroflow-adobe-macos/Gyroflow.plugin/Contents/MacOS

    echo "eFKTFXTC" >> gyroflow-adobe-macos/Gyroflow.plugin/Contents/PkgInfo
    /usr/libexec/PlistBuddy -c 'add CFBundlePackageType string eFKT' gyroflow-adobe-macos/Gyroflow.plugin/Contents/Info.plist
    /usr/libexec/PlistBuddy -c 'add CFBundleSignature string FXTC' gyroflow-adobe-macos/Gyroflow.plugin/Contents/Info.plist
    /usr/libexec/PlistBuddy -c 'add CFBundleIdentifier string xyz.gyroflow.adobe' gyroflow-adobe-macos/Gyroflow.plugin/Contents/Info.plist
    /usr/libexec/PlistBuddy -c "add CFBundleVersion string $version_short" gyroflow-adobe-macos/Gyroflow.plugin/Contents/Info.plist
    /usr/libexec/PlistBuddy -c "add CFBundleShortVersionString string $version" gyroflow-adobe-macos/Gyroflow.plugin/Contents/Info.plist

    cp x86_64-apple-darwin/deploy/gyroflow_adobe.rsrc gyroflow-adobe-macos/Gyroflow.plugin/Contents/Resources/Gyroflow.rsrc

    lipo {x86_64,aarch64}-apple-darwin/deploy/libgyroflow_adobe.dylib -create -output gyroflow-adobe-macos/Gyroflow.plugin/Contents/MacOS/Gyroflow.dylib
    mv gyroflow-adobe-macos/Gyroflow.plugin/Contents/MacOS/Gyroflow.dylib gyroflow-adobe-macos/Gyroflow.plugin/Contents/MacOS/Gyroflow

    codesign -vvvv --deep --strict --options=runtime --timestamp --force -s ${SIGNING_FINGERPRINT} gyroflow-adobe-macos/Gyroflow.plugin
    codesign -vvvv --deep --verify gyroflow-adobe-macos/Gyroflow.plugin

    ln -sf "/Library/Application Support/Adobe/Common/Plug-ins/7.0/MediaCore/" "gyroflow-adobe-macos/"
    hdiutil create "Gyroflow-Adobe-macos.dmg" -volname "Gyroflow Adobe plugin" -fs HFS+ -srcfolder "gyroflow-adobe-macos/" -ov -format UDZO -imagekey zlib-level=9

    codesign -vvvv --strict --options=runtime --timestamp --force -s ${SIGNING_FINGERPRINT} "Gyroflow-Adobe-macos.dmg"
    codesign -vvvv --deep --verify "Gyroflow-Adobe-macos.dmg"

[linux]
deploy:
    # No Adobe for linux
